;**********************************************************************;
;                                                                      ;
;   This file is part of FCSLIB, a file I/O library for the RSX180 OS. ;
;   Copyright (C) 1985-2020, Hector Peraza.                            ;
;                                                                      ;
;   This library is free software; you can redistribute it and/or      ;
;   modify it under the terms of the GNU Library General Public        ;
;   License as published by the Free Software Foundation; either       ;
;   version 2 of the License, or (at your option) any later version.   ;
;                                                                      ;
;   This library is distributed in the hope that it will be useful,    ;
;   but WITHOUT ANY WARRANTY; without even the implied warranty of     ;
;   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the      ;
;   GNU Library General Public License for more details.               ;
;                                                                      ;
;   You should have received a copy of the GNU Library General Public  ;
;   License along with this program; if not, write to the Free         ;
;   Software Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA. ;
;                                                                      ;
;**********************************************************************;

	.z80

	include	SYSFN.INC
	include	QIO.INC
	include	FCB.INC
	include	FCSLIB.INC

	public	FWRBLK

	extrn	?INC24,?CMP24,FRATTR,QIOBLK,QIOSB

	cseg

FWRBLK:	push	hl
	ld	l,(ix+FC.BUF)	; buffer address
	ld	h,(ix+FC.BUF+1)
	ld	(QIOBLK+Q.BUF),hl
	ld	l,(ix+FC.BLK)	; block number
	ld	h,(ix+FC.BLK+1)
	ld	(QIOBLK+Q.BLK),hl
	ld	l,(ix+FC.BLK+2)
	ld	h,(ix+FC.BLK+3)
	ld	(QIOBLK+Q.BLK+2),hl
	ld	l,(ix+FC.CNT)	; length
	ld	h,(ix+FC.CNT+1)
	ld	(QIOBLK+Q.LEN),hl
	ld	a,(ix+FC.LUN)	; LUN
	ld	(QIOBLK+Q.LUN),a
	ld	a,(ix+FC.EFN)	; event flag number
	ld	(QIOBLK+Q.EFN),a
	ld	hl,IO.WVB	; function code
	ld	(QIOBLK+Q.FUNC),hl
	ld	hl,0		; offset within block
	ld	(QIOBLK+Q.OFS),hl
	ld	(QIOBLK+Q.AST),hl
	ld	hl,QIOBLK
	SC	.QIO		; write block
	pop	hl
	ret	c
	ld	a,(QIOSB)
	ld	de,0
	or	a
	scf
	ret	nz
	ld	de,(QIOSB+2)
	res	FS.BM,(ix+FC.MODE) ; clear modified bit

	push	bc
	push	de
	push	hl
	ld	l,(ix+FC.FDB)
	ld	h,(ix+FC.FDB+1)
	ld	de,F.NUSED
	add	hl,de
	ld	e,(hl)		; get F.NUSED into CDE
	inc	hl
	ld	d,(hl)
	inc	hl
	ld	c,(hl)
	ld	hl,(QIOBLK+Q.BLK)
	ld	a,(QIOBLK+Q.BLK+2)
	ld	c,a
	ld	b,0
	call	?INC24		; BHL = blkno + 1
	call	?CMP24		; compare with F.NUSED
	jr	z,upd1		; branch if same (on last block)
	ld	a,0
	jr	nc,upd2		; return if within file limits
upd1:	xor	a
	bit	FM.DV,(ix+FC.MODE) ; device mode?
	call	z,FRATTR	; update file attributes if not (get new size)
upd2:	pop	hl
	pop	de
	pop	bc
	ret

	end
